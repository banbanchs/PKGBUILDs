# Maintainer: banbanchs <memory.silentvoyage@gmail.com>

pkgname=chinadns-c
pkgver=1.1.9
pkgrel=1
pkgdesc="Fix weird things with DNS in China."
url="https://github.com/clowwindy/ChinaDNS-C/tree/master"
arch=("i686" "x86_64")
license=("MIT")
provides=()
conflicts=()
depends=("glibc")
source=("git+https://github.com/clowwindy/ChinaDNS-C.git#tag=$pkgver"
        "chinadns.service"
        "chinadns@.service")
md5sums=("SKIP"
         "db901b2407d8c29b967cf2dc4d583544"
         "a1fa9eb3987fdbf8f9ff5443a7ace794")

build() {
  cd "ChinaDNS-C"
  ./autogen.sh
  ./configure --datarootdir=/etc --prefix=/usr
  make \
    CFLAGS="$CPPFLAGS $CFLAGS" \
    LDFLAGS="$LDFLAGS"
}

package() {
  cd "ChinaDNS-C"
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 "$srcdir/chinadns.service" "$pkgdir/usr/lib/systemd/system/chinadns.service"
  install -Dm644 "$srcdir/chinadns@.service" "$pkgdir/usr/lib/systemd/system/chinadns@.service"
}

# vim: ts=2 sw=2 et ft=sh
