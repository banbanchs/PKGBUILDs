# Maintainer: banbanchs <memory.silentvoyage@gmail.com>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=dnsmasq-chinadns
pkgver=2.72test3.27.g2d03bbc
pkgrel=1
pkgdesc="A patched version of dnsmasq which filters out some suspicious IP"
url="https://github.com/styx-hy/dnsmasq-chinadns"
arch=('i686' 'x86_64')
license=('GPL')
provides=('dnsmasq')
conflicts=('dnsmasq')
depends=('glibc' 'libdbus' 'gmp' 'nettle')
install=$pkgname.install
backup=('etc/dnsmasq.conf')
source=("git://github.com/styx-hy/dnsmasq-chinadns"
        'dnsmasq.service')
md5sums=('SKIP'
         '7ac45726cabef4145db40d758cc7fedf')

build() {
  cd "$pkgname"

  make \
    CFLAGS="$CPPFLAGS $CFLAGS" \
    LDFLAGS="$LDFLAGS" \
    COPTS="-DHAVE_DNSSEC -DHAVE_DBUS"
}

package() {
  cd "$pkgname"

  make BINDIR=/usr/bin PREFIX=/usr DESTDIR="$pkgdir" install

  install -Dm644 "dbus/dnsmasq.conf" "$pkgdir"/etc/dbus-1/system.d/dnsmasq.conf
  install -Dm644 "dnsmasq.conf.example" "$pkgdir"/etc/dnsmasq.conf
  install -Dm644 "$srcdir/dnsmasq.service" "$pkgdir"/usr/lib/systemd/system/dnsmasq.service
  install -Dm644 "spurious_ips.conf" "$pkgdir"/etc/spurious_ips.conf

  # DNSSEC setup
  sed -i 's,%%PREFIX%%,/usr,' "$pkgdir"/etc/dnsmasq.conf
  install -Dm644 "trust-anchors.conf" "$pkgdir"/usr/share/dnsmasq/trust-anchors.conf
}

# vim: ts=2 sw=2 et ft=sh
